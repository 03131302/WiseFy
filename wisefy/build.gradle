apply plugin: 'com.android.library'
apply plugin: 'com.getkeepsafe.dexcount'

group = rootProject.ext.GROUP
version = rootProject.ext.VERSION_NAME

dexcount {
    format = "tree"
    includeClasses = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
}

android {

    adbOptions {
        timeOutInMs 15 * 60 * 1000  // 15 minutes
        installOptions "-d","-t"
    }

    testOptions {
        // Allows Android code to be called from unit tests without causing a crash.
        // Helpful for allowing Log.d()-type calls
        unitTests.returnDefaultValues = true;
    }

    compileSdkVersion 26
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 10
        versionName version
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            testProguardFile file("proguard-test-rules.txt")
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:25.3.1'

    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'org.mockito:mockito-android:2.8.47'
    androidTestCompile 'com.android.support.test:runner:0.5'
    // For issue #55 / IllegalAccessError conflict test
    androidTestCompile 'com.google.android.gms:play-services-gcm:8.3.0'

    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'junit:junit:4.12'
}


apply from: rootProject.file('gradle/gradle-bintray-install.gradle')
apply from: rootProject.file('gradle/gradle-bintray-upload.gradle')
apply from: rootProject.file('gradle/jacoco.gradle')