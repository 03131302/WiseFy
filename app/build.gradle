apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'

def coverageSourceDirs = [
        '../app/src/main/java'
]

def classDirs = fileTree(
        dir: '../app/build/intermediates/classes/debug/com/',
        excludes: ['**/R.class',
                   '**/R$*.class',
                   '**/*$ViewBinder*.*',
                   '**/*MembersInjector*.*',
                   '**/BuildConfig.*',
                   '**/Manifest*.*',
                   '**/*$Lambda$*.class',
                   '**/*Factory*.class',
                   '**/*$Builder*'])

task jacocoDebugTestReport(type: JacocoReport, dependsOn: ["connectedDebugAndroidTest"]) {
    group = "Reporting"
    description = "Generate Jacoco coverage report for debug tests"
    classDirectories = classDirs
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files(['build/outputs/code-coverage/connected/coverage.ec'])

    onlyIf = {
        true
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dexcount {
    format = "tree"
    includeClasses = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 2
        versionName "1.0.1"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.3.0'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'

    testCompile 'junit:junit:4.12'
}