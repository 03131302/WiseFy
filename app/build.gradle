apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'

def coverageSourceDirs = [
        '../app/src/main/java'
]

def classDirs = fileTree(
        dir: '../app/build/intermediates/classes/debug/com/',
        excludes: ['**/R.class',
                   '**/R$*.class',
                   '**/*$ViewBinder*.*',
                   '**/*MembersInjector*.*',
                   '**/BuildConfig.*',
                   '**/Manifest*.*',
                   '**/*$Lambda$*.class',
                   '**/*Factory*.class',
                   '**/*$Builder*'])

task jacocoDebugTestReport(type: JacocoReport, dependsOn: ["connectedDebugAndroidTest"]) {
    group = "Reporting"
    description = "Generate Jacoco coverage report for debug tests"
    classDirectories = classDirs
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files([fileTree(dir: 'build/outputs/code-coverage/connected', include: '**/*.ec')])

    onlyIf = {
        true
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dexcount {
    format = "tree"
    includeClasses = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = false
}

android {

    testOptions {
        // Allows Android code to be called from unit tests without causing a crash.
        // Helpful for allowing Log.d()-type calls
        unitTests.returnDefaultValues = true;
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 3
        versionName "1.0.2"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.1'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testCompile 'junit:junit:4.12'
}