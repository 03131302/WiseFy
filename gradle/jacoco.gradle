apply plugin: 'jacoco'

def coverageSourceDirs = [
        '../wisefy/src/main/java'
]

def classDirs = fileTree(
        dir: '../wisefy/build/intermediates/classes/debug/com/',
        excludes: ['**/R.class',
                   '**/R$*.class',
                   '**/*$ViewBinder*.*',
                   '**/*MembersInjector*.*',
                   '**/BuildConfig.*',
                   '**/Manifest*.*',
                   '**/*$Lambda$*.class',
                   '**/*Factory*.class',
                   '**/*$Builder*'])

task jacocoDebugTestReport(type: JacocoReport, dependsOn: ["connectedDebugAndroidTest"]) {
    group = "Reporting"
    description = "Generate Jacoco coverage report for debug tests"
    classDirectories = classDirs
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files([fileTree(dir: 'build/outputs/code-coverage/connected', include: '**/*.ec')])

    onlyIf = {
        true
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }
}